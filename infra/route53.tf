data "aws_route53_zone" "main" {
  name         = local.domain_name
  private_zone = false
}

resource "aws_route53_record" "app" {
  zone_id         = data.aws_route53_zone.main.id
  name            = "${local.subdomain}.${local.domain_name}"
  type            = "A"
  allow_overwrite = true

  alias {
    name                   = aws_lb.app.dns_name
    zone_id                = aws_lb.app.zone_id
    evaluate_target_health = true
  }
}

resource "aws_acm_certificate" "app" {
  domain_name       = "${local.subdomain}.${local.domain_name}"
  validation_method = "DNS"
}

resource "aws_route53_record" "app_cert_validation" {
  for_each = {
    for dvo in aws_acm_certificate.app.domain_validation_options :
    dvo.domain_name => {
      name   = dvo.resource_record_name
      type   = dvo.resource_record_type
      record = dvo.resource_record_value
    }
  }

  zone_id = data.aws_route53_zone.main.id
  name    = each.value.name
  type    = each.value.type
  ttl     = 60

  records = [each.value.record]
}

resource "aws_acm_certificate_validation" "app" {
  certificate_arn = aws_acm_certificate.app.arn

  validation_record_fqdns = [
    for record in aws_route53_record.app_cert_validation : record.fqdn
  ]
}
